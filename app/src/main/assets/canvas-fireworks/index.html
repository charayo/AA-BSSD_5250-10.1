<!DOCTYPE HTML>
<html>
	<head>
		<meta charset="UTF-8">
		<script type="text/javascript" charset="utf8" src="./renderer.js"></script>
	</head>
	<body onload="init()">
		<canvas id="my-canvas" width="960" height="720"/>
		<script id="vertex-shader" type="x-shader/x-vertex">
attribute vec2 aVertexPosition;

void main() {
	gl_Position = vec4(aVertexPosition, 0, 1);
}
		</script>
		<script id="fragment-shader" type="x-shader/x-fragment">
precision mediump float;

uniform int time;
uniform int width;
uniform int height;

const int NUMBER_OF_STREAMS = 15;
const int PARTICLES_PER_STREAM = 10;
const float PARTICLE_LIGHT_RADIUS = 12.0;
const float PARTICLE_FLASH_RADIUS = 3.0;
const int NUMBER_OF_FRAMES = 100;
const float GRAVITY = 9.81 * 10.0;

const float PI = 3.141592653589793238462643383;
const float angle_change = 2.0 * PI / float(NUMBER_OF_STREAMS);

float angleByIndex(int index) {
	float angle = 2.0 * PI * float(index) / float(NUMBER_OF_STREAMS);

	/* Angle calculations are meant to start from the top, but coordinate axes start from the right; this adjusts for that difference. This prevents some shapes like pentagons from looking sideways and being vertically symmetrical instead of horizontally, which is more appealing. */
	angle += PI / 2.0;
	
	/* In addition, if there are an even number of particles, we should shift the first particle left half an angle change. */
	if (mod(float(NUMBER_OF_STREAMS), 2.0) == 0.0)
		angle += angle_change / 2.0;
	
	return angle;
}

void main() {
	float animation_progress = float(time) / float(NUMBER_OF_FRAMES);

	float center_x = float(width) / 2.0;
	float center_y = float(height) / 2.0;
	
	float final_radius = min(float(width), float(height)) / 3.0 /* r=screen/3; 2/3 of the screen will be used */;
	float radius = final_radius * animation_progress;

	/* Iterate over each particle of the firework to see how much color it adds to this pixel.
	WARNING: This could possibly be made more efficient!
	The firework particles follow paths according to evenly spaced angles on concentric circles; therefore, the number of concentric circles will be the number of particles on a stream and the number of streams will determine the angles by which the particles are positioned on the concentric circles. We then factor in gravity to determine the final position of all the circles. */
	float color_intensity = 0.0;
	float flash_intensity = 0.0;
	for (int circle = 1; circle <= PARTICLES_PER_STREAM; circle++) {
		float particle_circle_radius = radius * float(circle) / float(PARTICLES_PER_STREAM);
		
		/* the initial velocity of a particle used to calculate gravity */
		float v_0 = final_radius / float(NUMBER_OF_FRAMES) * float(circle) / float(PARTICLES_PER_STREAM);
		
		for (int yaw_index = 0; yaw_index < NUMBER_OF_STREAMS / 2; yaw_index++) {
			float yaw = angleByIndex(yaw_index);
			
			for (int pitch_index = 0; pitch_index < NUMBER_OF_STREAMS; pitch_index++) {
				float pitch = angleByIndex(pitch_index);
			
				float particle_x = center_x + particle_circle_radius * cos(pitch) * cos(yaw);
			
				float v_0y = v_0 * sin(pitch);
				float particle_y = center_y + particle_circle_radius * sin(pitch)
					- (/* gravity = g_y*t^2/2 + v_0y*t */ GRAVITY * animation_progress * animation_progress / 2.0 + v_0y * animation_progress);
		
				vec2 particle_position = vec2(particle_x, particle_y);
		
				color_intensity += max(0.0,
					(float(PARTICLE_LIGHT_RADIUS) - distance(particle_position, gl_FragCoord.xy)) / float(PARTICLE_LIGHT_RADIUS));
				flash_intensity += max(0.0,
					(float(PARTICLE_FLASH_RADIUS) - distance(particle_position, gl_FragCoord.xy)) / float(PARTICLE_FLASH_RADIUS));
			}
		}
	}
	color_intensity = min(1.0, color_intensity);
	flash_intensity = min(1.0, flash_intensity);
	
	// TODO TEMP: change this to a uniform of varying at some point
	const vec3 COLOR = vec3(1.0, 0.0, 0.0);

	// output pixel color
	gl_FragColor = vec4(COLOR * color_intensity, 1.0) + vec4(vec3(flash_intensity), 1.0);
}
		</script>
	</body>
</html>
